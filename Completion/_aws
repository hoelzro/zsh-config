#compdef aws secretsmanager

declare -ga __aws_services
declare -ga __aws_global_options
declare -gA __aws_service_commands
declare -gA __aws_service_command_options

__aws_services=$(COMP_LINE='aws ' COMP_POINT=4 aws_completer | head -n -1)
__aws_global_options=$(COMP_LINE='aws --' COMP_POINT=6 aws_completer | head -n -1)

function _aws() {
    local cmd

    if [[ $service = aws ]] ; then
        service=''
    fi

    for ((i=2; i < CURRENT; i++)) ; do
        if [[ "${words[$i]:0:2}" != '--' ]] ; then
            if [[ -z $service ]] ; then
                service=$words[$i]
            else
                cmd=$words[$i]
                break
            fi
        fi
    done

    if [[ -z $service ]] ; then
        _arguments "*:service:(${__aws_services[*]} ${__aws_global_options[*]})"
    else
        if [[ -z $cmd ]] ; then
            if [[ -z ${__aws_service_commands[(I)$service]} ]] ; then
                __aws_service_commands[$service]=$(COMP_LINE="aws $service " COMP_POINT=$((6 + ${#service} )) aws_completer | head -n -1)
            fi
            _arguments "*:$service commands:(${__aws_service_commands[$service]})"
        else
            if [[ -z ${__aws_service_command_options[(I)$service$cmd]} ]] ; then
                __aws_service_command_options[$service$cmd]=$(COMP_LINE="aws $service $cmd --" COMP_POINT=$((9 + ${#service} + ${#cmd} )) aws_completer | head -n -1)
            fi
            _arguments "*:$service $cmd options:(${__aws_service_command_options[$service$cmd]})"
        fi
    fi
}

_aws "$@"
