#compdef aws

declare -ga __aws_services
declare -ga __aws_global_options
declare -gA __aws_service_commands
declare -gA __aws_service_command_options

__aws_services=$(python -m awscli.completer 'aws ' 4 | head -n -1)
__aws_global_options=$(python -m awscli.completer 'aws --' 6 | head -n -1)

function _aws() {
    local service
    local cmd

    for ((i=2; i < CURRENT; i++)) ; do
        if [[ "${words[$i]:0:2}" != '--' ]] ; then
            if [[ -z $service ]] ; then
                service=$words[$i]
            else
                cmd=$words[$i]
                break
            fi
        fi
    done

    if [[ -z $service ]] ; then
        _arguments "*:service:(${__aws_services[*]} ${__aws_global_options[*]})"
    else
        if [[ -z $cmd ]] ; then
            if [[ -z ${__aws_service_commands[(I)$service]} ]] ; then
                __aws_service_commands[$service]=$(python -m awscli.completer "aws $service " $((6 + ${#service} )) | head -n -1)
            fi
            _arguments "*:$service commands:(${__aws_service_commands[$service]})"
        else
            if [[ -z ${__aws_service_command_options[(I)$service$cmd]} ]] ; then
                __aws_service_command_options[$service$cmd]=$(python -m awscli.completer "aws $service $cmd --" $((9 + ${#service} + ${#cmd} )) | head -n -1)
            fi
            _arguments "*:$service $cmd options:(${__aws_service_command_options[$service$cmd]})"
        fi
    fi
}

_aws "$@"
